plugins {
    id 'java'
    id('xyz.jpenilla.run-paper') version '2.3.1'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'idea'
}

group = 'Blue-Millennium'
version = '1.0.4'

idea {
    module {
        downloadSources = true
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/google' }
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'top.mrxiaom.mirai:overflow-core-api:1.0.5'
    implementation 'top.mrxiaom.mirai:overflow-core:1.0.5'
    implementation 'net.mamoe:mirai-core-api:2.16.0'
    implementation 'org.glavo:rcon-java:3.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.19.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.1'
    implementation 'com.electronwill.night-config:toml:3.8.2'
}

tasks {
  runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion('1.20.1')
  }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    destinationDirectory = file('run/plugins')
}
